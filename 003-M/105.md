Muscular Jade Hedgehog

Medium

# No slippage parameter for standard redemption request.


## Summary

No slippage parameter for standard redemption request. Users may receive less `tokenOut` than they expect.

## Vulnerability Detail

When users are redeeming mTokens to acquire `tokenOut`, there are two paths: 1) instant redemption, 2) standard redemption.

- During instant redemption, users will receive `tokenOut` immediately, based on the mToken rate and `tokenOut` rate at the time.
- During standard redemption, users will need to wait for admins to approve the request.

Users may fallback to using a standard redemption if the daily limit is hit for instant redemption.

The issue here is that there is a slippage parameter limiting the amount of `tokenOut` users receive for instant redemption, but not for standard redemption. This is an issue, especially considering the `tokenOut` rate is calculated during the redemption request transaction, and that there is no slippage parameter to control it. Example:

1. User calls `redeemRequest()` to redeem mToken for WBTC.
2. The redemption transaction takes a while to process, and WBTC price bursts during the time (and recovers later).
3. The transaction is processed, but the `tokenOutRate` is now much higher than when the user initiated the redemption. This will lead to users receiving less `tokenOut` in the end.

During the process, there is no mechanism for user to set a slippage parameter nor cancel the request.

Note that the specs did not mention slippage is required for standard redemption. However, this doesn't mean it doesn't pose a vulnerability.

```solidity
    function _redeemRequest(address tokenOut, uint256 amountMTokenIn)
        internal
        returns (uint256)
    {
        address user = msg.sender;

        bool isFiat = tokenOut == MANUAL_FULLFILMENT_TOKEN;

        (
            uint256 feeAmount,
            uint256 amountMTokenWithoutFee
        ) = _calcAndValidateRedeem(
                user,
                tokenOut,
                amountMTokenIn,
                false,
                isFiat
            );

        address tokenOutCopy = tokenOut;

        uint256 tokenOutRate;
        if (!isFiat) {
            TokenConfig storage config = tokensConfig[tokenOutCopy];
@>          tokenOutRate = _getTokenRate(config.dataFeed, config.stable);
        }

        uint256 amountMTokenInCopy = amountMTokenIn;

        uint256 mTokenRate = mTokenDataFeed.getDataInBase18();

        _tokenTransferFromUser(
            address(mToken),
            address(this),
            amountMTokenWithoutFee,
            18 // mToken always have 18 decimals
        );
        if (feeAmount > 0)
            _tokenTransferFromUser(address(mToken), feeReceiver, feeAmount, 18);

        uint256 requestId = currentRequestId.current();
        currentRequestId.increment();

        redeemRequests[requestId] = Request({
            sender: user,
            tokenOut: tokenOutCopy,
            status: RequestStatus.Pending,
            amountMToken: amountMTokenWithoutFee,
            mTokenRate: mTokenRate,
@>          tokenOutRate: tokenOutRate
        });

        emit RedeemRequest(requestId, user, tokenOutCopy, amountMTokenInCopy);

        return requestId;
    }


    function _approveRequest(
        uint256 requestId,
        uint256 newMTokenRate,
        bool isSafe
    ) internal {
        ...

        if (request.tokenOut != MANUAL_FULLFILMENT_TOKEN) {
            uint256 tokenDecimals = _tokenDecimals(request.tokenOut);

            uint256 amountTokenOutWithoutFee = _truncate(
@>              (request.amountMToken * newMTokenRate) / request.tokenOutRate,
                tokenDecimals
            );

            _tokenTransferFromTo(
                request.tokenOut,
                requestRedeemer,
                request.sender,
                amountTokenOutWithoutFee,
                tokenDecimals
            );
        }
        ...
    }
```

## Impact

Users may receive less `tokenOut` than they expect.

## Code Snippet

- https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVault.sol#L372

## Tool used

Manual Review

## Recommendation

Also add a slippage parameter for standard redemption to limit the minimum amount of `tokenOut` received.

When admins approve the redemption request, check the amount of `tokenOut` against the number. If it is impossible to fulfill the request, the admin can just reject the request.