Cheerful Porcelain Eel

Medium

# Not considering BUIDL transfer configurations can lead to DoS in BUIDL redemption vault

## Summary

Current interaction with `buidlRedemption` in the `RedemptionVaultWithBUIDL` and `MBasisRedemptionVaultWithBUIDL` contracts does not consider certain behaviors from the BUIDL token if some configurations are enabled. Because of this, the BUIDL redemption vaults could misbehave, leading to a DoS in the `redeemInstant` functionality involving BUIDL redemptions.

## Vulnerability Detail

## Bug description

Midas’ README in Sherlock’s page includes [a notion site](https://www.notion.so/8060186191934380800b669406f4d83c?pvs=21) with several invariants that must hold to consider that the system is behaving as expected. The Midas team encourages to report any discrepancy as a bug: “*Product Requirements (Specifications). Please note that discrepancies between the spec and the code can be reported as issues*”.

One of the pages in the invariants documents references the functionality of redeeming mTBILL for USDC pulled from BUIDL. Inside it, the following comment is found regarding an edge case: “*Have a look at the edge cases that Ondo had to handle during their C4 audit, and implement the handling of relevant edge cases they are handling.”*

This references [this finding](https://code4rena.com/reports/2024-03-ondo-finance#m-01-integration-issue-in-ousginstantmanager-with-buidl-if-minustokens-is-set-by-blackrock) from Code4rena’s Ondo finance audit, which highlights an integration issue with the BUIDL token due to not considering one of its configurations. Although this is explicitly mentioned in the invariants document, the edge cases were never handled in Midas’ code, leading to some issues in the code.

### A brief summary about BUIDL’s token implementation

The BUIDL token incorporates a wide range of possible configurations that affect its behavior when being transferred/interacted with. A good example of this weird behavior can be found in the `transfer`/`transferFrom` functions from BUIDL ([code for reference](https://etherscan.deth.net/address/0x603bb6909be14f83282e03632280d91be7fb83b2)). In it, we can see that both of them incorporate a modifier called `canTransfer`, which performs certain validations prior to any BUIDL token being transferred via the ComplianceService’s `validateTransfer` function:

```solidity
// DSToken.sol (BUIDL token implementation)

modifier canTransfer(address _sender, address _receiver, uint256 _value) {
        getComplianceService().validateTransfer(_sender, _receiver, _value, paused, super.balanceOf(_sender));
        _;
    }

function transfer(address _to, uint256 _value) public virtual override canTransfer(msg.sender, _to, _value) returns (bool) {
    return postTransferImpl(super.transfer(_to, _value), msg.sender, _to, _value);
}

 
function transferFrom(address _from, address _to, uint256 _value) public virtual override canTransfer(_from, _to, _value) returns (bool) {
    return postTransferImpl(super.transferFrom(_from, _to, _value), _from, _to, _value);
}
```

The `validateTransfer` function in the ComplianceService contract  will then trigger the `newPreTransferCheck` function in ComplianceServiceRegulated:

```solidity
// ComplianceService (abstract, inherited by ComplianceServiceRegulated)

function validateTransfer(
        address _from,
        address _to,
        uint256 _value,
        bool _paused,
        uint256 _balanceFrom
    ) public virtual override onlyToken returns (bool) {
        uint256 code;
        string memory reason;

        (code, reason) = newPreTransferCheck(_from, _to, _value, _balanceFrom, _paused);
        require(code == 0, reason);

        return recordTransfer(_from, _to, _value);
    }
```

Finally, the `newPreTransferCheck` function will trigger the internal `doPreTransferCheckRegulated` function, which performs a set of global configuration checks to ensure that the transfer being carried out is correct by finally triggering `completeTransferCheck`:

<details>
    <summary>`ComplianceServiceRegulated.sol` implementation</summary>
    
    ```solidity
    // ComplianceServiceRegulated.sol
    
    function newPreTransferCheck(
            address[] memory _services,
            address _from,
            address _to,
            uint256 _value,
            uint256 _balanceFrom,
            bool _paused
        ) public view returns (uint256 code, string memory reason) {
            return doPreTransferCheckRegulated
            (_services, _from, _to, _value, _balanceFrom, _paused);
        }
    
    function doPreTransferCheckRegulated(
            address[] memory _services,
            address _from,
            address _to,
            uint256 _value,
            uint256 _balanceFrom, 
            bool _paused
        ) internal view returns (uint256 code, string memory reason) {
    
            if (_balanceFrom < _value) {
                return (15, NOT_ENOUGH_TOKENS);
            }
    
            uint256 fromInvestorBalance = balanceOfInvestor(_services, _from);
            uint256 fromRegion = getCountryCompliance(_services, _from);
            bool isPlatformWalletTo = IDSWalletManager(_services[WALLET_MANAGER]).isPlatformWallet(_to);
            if (isPlatformWalletTo) { 
                if (
                    ((IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getForceFullTransfer()
                    && (fromRegion == US)) ||
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getWorldWideForceFullTransfer()) &&
                    fromInvestorBalance > _value
                ) {
                    return (50, ONLY_FULL_TRANSFER);
                }
                return (0, VALID);
            }
    
            if (_paused && !(isOmnibusTBE(IDSOmnibusTBEController(_services[OMNIBUS_TBE_CONTROLLER]), _from))) {
                return (10, TOKEN_PAUSED);
            }
    
            CompletePreTransferCheckArgs memory args = CompletePreTransferCheckArgs(_from, _to, _value, fromInvestorBalance, fromRegion, isPlatformWalletTo);
            return completeTransferCheck(_services, args);
        }
        
        function completeTransferCheck(
            address[] memory _services,
            CompletePreTransferCheckArgs memory _args
        ) internal view returns (uint256 code, string memory reason) {
            (string memory investorFrom, string memory investorTo) = IDSRegistryService(_services[REGISTRY_SERVICE]).getInvestors(_args.from, _args.to);
            if (
                !CommonUtils.isEmptyString(investorFrom) && CommonUtils.isEqualString(investorFrom, investorTo)
            ) {
                return (0, VALID);
            }
    
            if (!ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).checkWhitelisted(_args.to)) {
                return (20, WALLET_NOT_IN_REGISTRY_SERVICE);
            }
    
            uint256 toRegion = getCountryCompliance(_services, _args.to);
            if (toRegion == FORBIDDEN) {
                return (26, DESTINATION_RESTRICTED);
            }
    
            if (isOmnibusTBE(IDSOmnibusTBEController(_services[OMNIBUS_TBE_CONTROLLER]), _args.from)) {
                return(0, VALID);
            }
     
            bool isPlatformWalletFrom = IDSWalletManager(_services[WALLET_MANAGER]).isPlatformWallet(_args.from);
            if (
                !isPlatformWalletFrom &&
            IDSLockManager(_services[LOCK_MANAGER]).getTransferableTokens(_args.from, block.timestamp) < _args.value
            ) {
                return (16, TOKENS_LOCKED);
            } 
    
            if (_args.fromRegion == US) {
                if (checkHoldUp(_services, _args.from, _args.value, true, isPlatformWalletFrom)) {
                    return (32, HOLD_UP);
                }
    
                if (
                    _args.fromInvestorBalance > _args.value &&
                    _args.fromInvestorBalance - _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinUSTokens()
                ) {
                    return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
                }
    
                if (
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getForceFullTransfer() &&
                    _args.fromInvestorBalance > _args.value
                ) {
                    return (50, ONLY_FULL_TRANSFER);
                }
            } else {
                if (checkHoldUp(_services, _args.from, _args.value, false, isPlatformWalletFrom)) {
                    return (33, HOLD_UP);
                }
    
                if (
                    toRegion == US &&
                    !isPlatformWalletFrom &&
                    isBlockFlowbackEndTimeOk(IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getBlockFlowbackEndTime())
                ) {
                    return (25, FLOWBACK);
                }
    
                if (
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getWorldWideForceFullTransfer() && 
                    _args.fromInvestorBalance > _args.value
                ) {
                    return (50, ONLY_FULL_TRANSFER);
                }
            }
    
            uint256 toInvestorBalance = balanceOfInvestor(_services, _args.to);
            string memory toCountry = getCountry(_services, _args.to);
    
            if (_args.fromRegion == EU) {
                if (_args.fromInvestorBalance - _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinEUTokens() &&
                    _args.fromInvestorBalance > _args.value) {
                    return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
                }
            }
    
            bool isAccreditedTo = isAccredited(_services, _args.to);
            if (
                IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getForceAccredited() && !isAccreditedTo
            ) {
                return (61, ONLY_ACCREDITED);
            }
    
            if (toRegion == JP) {
                if (
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getJPInvestorsLimit() != 0 &&
                    ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).getJPInvestorsCount() >=
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getJPInvestorsLimit() &&
                    isNewInvestor(_services, _args.to, toInvestorBalance) &&
                    (!CommonUtils.isEqualString(getCountry(_services, _args.from), toCountry) || (_args.fromInvestorBalance > _args.value))
                ) {
                    return (40, MAX_INVESTORS_IN_CATEGORY);
                }
            } else if (toRegion == EU) {
                if (
                    isRetail(_services, _args.to) &&
                    ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).getEURetailInvestorsCount(toCountry) >=
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getEURetailInvestorsLimit() &&
                    isNewInvestor(_services, _args.to, toInvestorBalance) &&
                    (!CommonUtils.isEqualString(getCountry(_services, _args.from), toCountry) ||
                    (_args.fromInvestorBalance > _args.value && isRetail(_services, _args.from)))
                ) {
                    return (40, MAX_INVESTORS_IN_CATEGORY);
                }
    
                if (
                    toInvestorBalance + _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinEUTokens()
                ) {
                    return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
                }
            } else if (toRegion == US) {
                if (
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getForceAccreditedUS() &&
                    !isAccreditedTo
                ) {
                    return (62, ONLY_US_ACCREDITED);
                }
    
                uint256 usInvestorsLimit = getUSInvestorsLimit(_services);
                if (
                    usInvestorsLimit != 0 &&
                    _args.fromInvestorBalance > _args.value &&
                    ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).getUSInvestorsCount() >= usInvestorsLimit &&
                    isNewInvestor(_services, _args.to, toInvestorBalance)
                ) {
                    return (40, MAX_INVESTORS_IN_CATEGORY);
                }
    
                if (
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getUSAccreditedInvestorsLimit() != 0 &&
                    isAccreditedTo &&
                    ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).getUSAccreditedInvestorsCount() >=
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getUSAccreditedInvestorsLimit() &&
                    isNewInvestor(_services, _args.to, toInvestorBalance) &&
                    (_args.fromRegion != US || !isAccredited(_services, _args.from) || _args.fromInvestorBalance > _args.value)
                ) {
                    return (40, MAX_INVESTORS_IN_CATEGORY);
                }
    
                if (
                    toInvestorBalance + _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinUSTokens()
                ) {
                    return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
                }
            }
    
            if (!isAccreditedTo) {
                if (maxInvestorsInCategoryForNonAccredited(_services, _args.from, _args.to, _args.value, _args.fromInvestorBalance, toInvestorBalance)) {
                    return (40, MAX_INVESTORS_IN_CATEGORY);
                }
            }
    
            if (
                IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getTotalInvestorsLimit() != 0 &&
                _args.fromInvestorBalance > _args.value &&
                ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).getTotalInvestorsCount() >=
                IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getTotalInvestorsLimit() &&
                isNewInvestor(_services, _args.to, toInvestorBalance)
            ) {
                return (40, MAX_INVESTORS_IN_CATEGORY);
            }
    
            if (
                _args.fromInvestorBalance == _args.value &&
                !isNewInvestor(_services, _args.to, toInvestorBalance) &&
                ComplianceServiceRegulated(_services[COMPLIANCE_SERVICE]).getTotalInvestorsCount() <=
                IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinimumTotalInvestors()
            ) {
                return (71, NOT_ENOUGH_INVESTORS);
            }
    
            if (
                !isPlatformWalletFrom &&
            _args.fromInvestorBalance - _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinimumHoldingsPerInvestor() &&
            _args.fromInvestorBalance > _args.value
            ) {
                return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
            }
    
            if (
                !_args.isPlatformWalletTo &&
            toInvestorBalance + _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinimumHoldingsPerInvestor()
            ) {
                return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
            }
    
            if (
                isMaximumHoldingsPerInvestorOk(
                    IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMaximumHoldingsPerInvestor(),
                    toInvestorBalance, _args.value)
            ) {
                return (52, AMOUNT_OF_TOKENS_ABOVE_MAX);
            }
    
            return (0, VALID);
        }
    ```
</details>

### How enabling some configurations in BUIDL will affect Midas’ `RedemptionVaultWithBUIDL`, leading to DoS

As it can be seen in BUIDL’s summary, there’s a wide range of configurations that could affect the behavior of the `RedemptionVaultWithBUIDL` vault. Although some configurations can’t actually be handled, and hence can’t be considered a bug (for example pausing the BUIDL token, as there’s nothing Midas can actually do given that if the token is paused the BUIDL functionality will simply not work), some others can (and **MUST**) be handled to ensure the correct integration of `BUIDL`.

The configuration mentioned in Code4rena’s report (and the consideration that explicitly asks for this scenario to be handled in Midas’ invariants document) can lead to a DoS in the `RedemptionVaultWithBUIDL` that could be avoided with the proper implementation.

This specific configuration refers to the `minUSTokens`/`minEUTokens` variables. Essentially, these variables enforce that the `from`/`to` addresses involved in a transfer of tokens **must** have a minimum of `minUSTokens`/`minEUTokens` after the transfer is being performed. As an example, let’s say that `from` belongs to the `EU` jurisdiction, and BUIDL owners decide to configure the `minEUTokens` to `100e6` . If `from` had a balance of `500e6` tokens, they would be able to transfer at most `400e6` tokens. Trying to transfer more tokens will make `from`'s balance be below the `minEUTokens` configured, making the transfer revert.

```solidity
// ComplianceServiceRegulated.sol

function completeTransferCheck(
        address[] memory _services,
        CompletePreTransferCheckArgs memory _args
    ) internal view returns (uint256 code, string memory reason) {
		
		...

		if (_args.fromRegion == EU) {
			if (_args.fromInvestorBalance - _args.value < IDSComplianceConfigurationService(_services[COMPLIANCE_CONFIGURATION_SERVICE]).getMinEUTokens() &&
			    _args.fromInvestorBalance > _args.value) {
			    return (51, AMOUNT_OF_TOKENS_UNDER_MIN);
			}
		}
        
		...
}
```

Currently, if there’s not enough USDC in the `RedemptionVaultWithBUIDL` to cover a redemption, the contract will try to redeem the user’s a specific `amountTokenOut` of `BUIDL` token to get the same `amountTokenOut` of USDC and succesfully finish the redemption:

```solidity
// RedemptionVaultWithBUIDL.sol
     
     function _checkAndRedeemBUIDL(address tokenOut, uint256 amountTokenOut)
        internal
    {
        uint256 contractBalanceTokenOut = IERC20(tokenOut).balanceOf(
            address(this)
        );
        
        if (contractBalanceTokenOut >= amountTokenOut) return;

        uint256 buidlToRedeem = amountTokenOut - contractBalanceTokenOut;

        buidl.safeIncreaseAllowance(address(buidlRedemption), buidlToRedeem);
        buidlRedemption.redeem(buidlToRedeem); 
    }
```

Internally, the call to `redeem` will transfer the exact amount of `BUIDL` tokens from the vault to a `recipient` address. This means that calls to `redeem` will trigger the `canTransfer` checks previously mentioned.

```solidity
// Redemption.sol 

 function redeem(
        uint256 amount
    ) external override whenNotPaused onlyAssetHolder {
        address recipient = settlement.recipient();

        // Transfer asset
        IERC20(asset).safeTransferFrom(msg.sender, recipient, amount);

        ...
    }
```

If a minimum amount of tokens to be held after transfers is configured in the `BUILD` token contract (and depending on `MIDAS` token contract jurisdiction), the call to redeem could fail. Taking the same example as before, let’s imagine that the `RedemptionVaultWithBUIDL` currently holds 500e6 `BUIDL` tokens, and the `BUIDL` token has been configured to enforce a minimum balance after transfers of 100e6 tokens. The `RedemptionVaultWithBUIDL` currently holds 0 USDC. This should allow a certain user to redeem 450e6 USDC by redeeming `BUIDL`, given that the current `BUIDL` contract balance is greater than the requested USDC amount. However, due to the minimum balance requirements enforced by `BUIDL`, the redeem function will fail, given that the minimum balance of the `RedemptionVaultWithBUIDL` after redeeming will be less than the minimum acceptable balance configured (`500e6` - `450e6` < `100e6`), making the transaction revert when in reality it should be possible to execute the transaction.


## Impact

Medium. As per the invariants document, it looks like the Midas’ team wanted to implement a mitigation for this issue, but ended up never implementing it. Not implementing the mitigation can lead to the previously mentioned situation, effectively causing a DoS for the `redeemInstant` function in the `RedemptionVaultWithBUIDL` and `MBasisRedemptionVaultWithBUIDL` contracts.

## Code Snippet

https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVaultWithBUIDL.sol#L175

## Tool used

Manual Review

## Recommendation

Withdraw the full balance of `BUIDL` tokens held in the `RedemptionVaultWithBUIDL` if a `BUIDL` redemption is required. This will prevent the transaction from reverting, as withdrawing the full balance makes the checks not be applied (due to balance becoming zero). This will not only be helpful to adequately handle `minEUTokens`/`minUSTokens` configurations, but also for several other configurations that might affect the contract’s behavior as well (such as the `forceFullTransfer` or `worldWideFullTransfer` configurations).

```diff
// RedemptionVaultWithBUIDL.sol

function _checkAndRedeemBUIDL(address tokenOut, uint256 amountTokenOut)
        internal
    {
        uint256 contractBalanceTokenOut = IERC20(tokenOut).balanceOf(
            address(this)
        );
        
        if (contractBalanceTokenOut >= amountTokenOut) return;

-        uint256 buidlToRedeem = amountTokenOut - contractBalanceTokenOut;

+        uint256 buidlBalance = buidl.balanceOf(address(this));

-        buidl.safeIncreaseAllowance(address(buidlRedemption), buidlToRedeem);
+        buidl.safeIncreaseAllowance(address(buidlRedemption), buidlBalance);

-        buidlRedemption.redeem(buidlToRedeem); 
+        buidlRedemption.redeem(buidlBalance); 

}
```