Cheerful Porcelain Eel

Medium

# Missing token allowance update when requesting redemptions will lead to redemption cap being bypassed

## Summary

`tokenOut` allowance is not updated when requesting/approving a redeem, making it possible to bypass the allowance caps configured.

## Vulnerability Detail

Midas includes an allowance for each configured token in the protocol. This is intended to act as a cap, i.e a limit on the amount of tokens that users use in the protocol.

Reducing the allowance for each token can always be done by calling the internal `_requireAndUpdateAllowance` function in the parent contract `ManageableVault`:

```solidity
// ManageableVault.sol

function _requireAndUpdateAllowance(address token, uint256 amount)
        internal
    { 
        uint256 prevAllowance = tokensConfig[token].allowance;
        if (prevAllowance == MAX_UINT) return;

        require(prevAllowance >= amount, "MV: exceed allowance");

        tokensConfig[token].allowance -= amount;
    }
```

Taking a look at `DepositVault`, we can see that allowance is updated **inside the** `_calcAndValidateDeposit`, an internal function called in both the instant and non-instant deposit flows**:**

```solidity
// DepositVault.sol

function _calcAndValidateDeposit(
        address user,
        address tokenIn,
        uint256 amountToken, 
        bool isInstant
    )
        internal
        returns (
            uint256 tokenAmountInUsd,
            uint256 feeTokenAmount,
            uint256 amountTokenWithoutFee,
            uint256 mintAmount,
            uint256 tokenInRate,
            uint256 tokenOutRate,
            uint256 tokenDecimals
        )
    {
        ...

        _requireAndUpdateAllowance(tokenIn, amountToken); 
        
        ...
		}  
```

As mentioned, both the instant and non-instant deposit flows call the `_calcAndValidateDeposit` function, which is correct:

```solidity
// DepositVault.sol

function depositInstant(
        address tokenIn,
        uint256 amountToken,
        uint256 minReceiveAmount,
        bytes32 referrerId
    )
        external
        whenFnNotPaused(this.depositInstant.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
    {
        ...

        (
            uint256 tokenAmountInUsd,
            uint256 feeTokenAmount,
            uint256 amountTokenWithoutFee,
            uint256 mintAmount,
            ,
            ,
            uint256 tokenDecimals
        ) = _calcAndValidateDeposit(user, tokenInCopy, amountTokenCopy, true);
				
				...
				
		}
		
		function depositRequest(
        address tokenIn,
        uint256 amountToken,
        bytes32 referrerId
    )
        external
        whenFnNotPaused(this.depositRequest.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
        returns (uint256 requestId)
    {
        ...

        (
            uint256 tokenAmountInUsd,
            uint256 feeAmount,
            uint256 amountTokenWithoutFee,
            ,
            uint256 tokenInRate,
            uint256 tokenOutRate,
            uint256 tokenDecimals
        ) = _calcAndValidateDeposit(user, tokenInCopy, amountTokenCopy, false);
        
        ...
        
     }
```

However, taking a look at the `RedemptionVault` contract, we can see that the allowance is **only updated for instant redeems:**

```solidity
// RedemptionVault.sol

function redeemInstant(
        address tokenOut,
        uint256 amountMTokenIn,
        uint256 minReceiveAmount
    ) 
        external
        virtual
        whenFnNotPaused(this.redeemInstant.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
    {
        ...
  
        _requireAndUpdateAllowance(tokenOutCopy, amountTokenOut); 
				
				...
				
		}
		
		
```

Non-instant (request) redeems **don’t update the `tokenOut` allowance,** enabling the allowance cap to be bypassed by performing redeem requests instead of redeeming instantly. This occurs because the internal `_calcAndValidateRedeem` does not update the allowance just like `_calcAndValidateDeposit` did for deposits in the `DepositVault`:

```solidity
// RedemptionVault.sol

function redeemRequest(address tokenOut, uint256 amountMTokenIn)
        external
        whenFnNotPaused(this.redeemRequest.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
        returns (uint256 requestId)
    {
        require(tokenOut != MANUAL_FULLFILMENT_TOKEN, "RV: tokenOut == fiat");
        return _redeemRequest(tokenOut, amountMTokenIn);
    }

    function redeemFiatRequest(uint256 amountMTokenIn)
        external
        whenFnNotPaused(this.redeemFiatRequest.selector)
        onlyAlwaysGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
        returns (uint256 requestId)
    {
        return _redeemRequest(MANUAL_FULLFILMENT_TOKEN, amountMTokenIn);
    }
    
    
    function _redeemRequest(address tokenOut, uint256 amountMTokenIn)
        internal
        returns (uint256)
    {
        address user = msg.sender;

        bool isFiat = tokenOut == MANUAL_FULLFILMENT_TOKEN;

        (
            uint256 feeAmount,
            uint256 amountMTokenWithoutFee
        ) = _calcAndValidateRedeem( 
                user,
                tokenOut,
                amountMTokenIn,
                false,
                isFiat
            );

        address tokenOutCopy = tokenOut;

        uint256 tokenOutRate;
        if (!isFiat) {
            TokenConfig storage config = tokensConfig[tokenOutCopy];
            tokenOutRate = _getTokenRate(config.dataFeed, config.stable);
        }

        uint256 amountMTokenInCopy = amountMTokenIn;

        uint256 mTokenRate = mTokenDataFeed.getDataInBase18();

        _tokenTransferFromUser(
            address(mToken),
            address(this),
            amountMTokenWithoutFee,
            18 // mToken always have 18 decimals
        );
        
        if (feeAmount > 0)
            _tokenTransferFromUser(address(mToken), feeReceiver, feeAmount, 18);

        uint256 requestId = currentRequestId.current();
        currentRequestId.increment();

        redeemRequests[requestId] = Request({
            sender: user,
            tokenOut: tokenOutCopy,
            status: RequestStatus.Pending,
            amountMToken: amountMTokenWithoutFee,
            mTokenRate: mTokenRate,
            tokenOutRate: tokenOutRate
        });

        emit RedeemRequest(requestId, user, tokenOutCopy, amountMTokenInCopy);

        return requestId;
    }
```

As shown in the code snippet, the `_redeemRequest` never reduces the allowance, effectively enabling the allowance cap to be bypassed via redeem requests.

Besides this, the token allowance update is also missing in the `MBasisRedemptionVaultWithSwapper` contract when performing instant requests:

```solidity
// MBasisRedemptionVaultWithSwapper.sol

function redeemInstant(
        address tokenOut,
        uint256 amountMTokenIn, 
        uint256 minReceiveAmount
    )
        external
        override(IRedemptionVault, RedemptionVault)
        whenFnNotPaused(this.redeemInstant.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender) 
        onlyNotSanctioned(msg.sender)
    {
        ...

        if (
            contractTokenOutBalance >=
            amountTokenOutWithoutFee.convertFromBase18(tokenDecimals)
        ) {
            _requireAndUpdateLimit(amountMTokenInCopy); 
            _requireAndUpdateAllowance(tokenOutCopy, amountTokenOut);

            mToken.burn(user, amountMTokenWithoutFee);
        } else {
            uint256 mTbillAmount = _swapMBasisToMToken(amountMTokenWithoutFee); 

            IERC20(mTbillRedemptionVault.mToken()).safeIncreaseAllowance(
                address(mTbillRedemptionVault),
                mTbillAmount
            );

            mTbillRedemptionVault.redeemInstant( 
                tokenOutCopy,
                mTbillAmount,
                minReceiveAmountCopy
            );
  
            uint256 contractTokenOutBalanceAfterRedeem = IERC20(tokenOutCopy)
                .balanceOf(address(this));

            
            amountTokenOutWithoutFee = (contractTokenOutBalanceAfterRedeem -
                contractTokenOutBalance).convertToBase18(tokenDecimals);
        }
        
        ...
        
}

```

Checking the code snippet we can see the `_requireAndUpdateAllowance` call to update the allowance is only performed when the contract has enough USDC so that the swap is avoided. However, if the swap flow needs to be triggered, neither the allowance nor the instant limits will be updated, which is incorrect as per the requirements for the contract. In addition, `_requireAndUpdateLimit` is also not called, so instant daily limit is also not updated in the swap flow.

## Impact

Medium. The allowance cap won’t work as expected for redeems, allowing users to redeem a higher amount than the expected. The correct behavior of mint/redeem caps is explicitly mentioned in Midas’ Sherlock README, linking to the [external document where all invariants are detailed](https://www.notion.so/Admin-manages-the-list-of-token_in-and-token_out-477e48c7688b443aa496a669134725f0?pvs=21), where a *“redeem cap (allowance based, set in token_out)”* is explicitly mentioned in the *“Admin manages the list of token_in and token_out”* section. Hence why this issue should be considered as Medium severity.

## Code Snippet

https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVault.sol#L390

## Tool used

Manual Review

## Recommendation

Update the allowance cap when requesting redeems. Note that this should be done in two steps to properly compute the allowance:

- Firstly, it should be done inside the internal `_redeemRequest` amount by computing the corresponding `tokenOut` amount, utilizing the `tokenOutRate` obtained and the USD value of the mTokens redeemed at the time of performing the request.
- Secondly, if the request is approved the new amount of `tokenOut` should be computed, and the difference between the amount at the time of the request approval and the amount computed in the actual request should also be reduced from the allowance. This is because the total amount of `tokenOut` can differ if the new rate passed when approving a request makes the overall USD value of the redeemed mTokens increase.

Note that there’s an edge case where the rate of the mTokens actually decreases between the redeem request and its approval. In that situation, the allowance should be **increased instead of decreased**, as the final transferred amount of `tokenOut` will be lower than initially expected.

Besides redeem requests, also update the allowance and instant daily limits when the swap flow is triggered in the `MBasisRedemptionVaultWithSwapper` contract.