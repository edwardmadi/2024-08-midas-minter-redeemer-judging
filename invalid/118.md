Cheerful Porcelain Eel

High

# Lack of sanctioned user checks when minting makes it possible to mint tokens to users who become sanctioned between deposit request and request approval

## Summary

Users sanctioned in Chainalysis’ oracle between a deposit/redeem request and its acceptance can still get their request accepted, allowing sanctioned users to interact with the protocol.

## Vulnerability Detail

Midas includes some restrictions in order to prevent addresses sanctioned in Chainalysis’ oracle from depositing to or redeeming from the vaults. These restrictions are not only implemented in instant actions, but also in deposit/redeem requests via the `onlyNotSanctioned` modifier:

```solidity
// DepositVault.sol

function depositRequest(
        address tokenIn,
        uint256 amountToken,
        bytes32 referrerId
    )
        external
        whenFnNotPaused(this.depositRequest.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender) 
        returns (uint256 requestId)
    {
    
    ...
    
    }
```

```solidity
// RedemptionVault.sol

function redeemRequest(address tokenOut, uint256 amountMTokenIn)
        external
        whenFnNotPaused(this.redeemRequest.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
        returns (uint256 requestId)
    {
        require(tokenOut != MANUAL_FULLFILMENT_TOKEN, "RV: tokenOut == fiat");
        return _redeemRequest(tokenOut, amountMTokenIn);
    }

   ...
   
    function redeemFiatRequest(uint256 amountMTokenIn)
        external
        whenFnNotPaused(this.redeemFiatRequest.selector)
        onlyAlwaysGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
        returns (uint256 requestId)
    {
        return _redeemRequest(MANUAL_FULLFILMENT_TOKEN, amountMTokenIn);
    }
```

As shown in the code snippet, users who are blacklisted, not greenlisted (when greenlist is active) or sanctioned via Chainalysis’ oracle **shouldn’t be able to** **obtain mTBILL/mBASIS tokens** via interaction with the vaults.

The problem with the current implementation is that deposit/redeem request approvals don’t include checks to ensure that users have not been sanctioned by Chainalysis between the deposit/redeem request and the approval of it. As shown in the following snippet, no checks are performed when approving requests:

```solidity
// DepositVault.sol

function safeApproveRequest(uint256 requestId, uint256 newOutRate)
        external
        onlyVaultAdmin
    {
        _approveRequest(requestId, newOutRate, true);

        emit SafeApproveRequest(requestId, newOutRate);
    }

    function approveRequest(uint256 requestId, uint256 newOutRate)
        external
        onlyVaultAdmin
    {
        _approveRequest(requestId, newOutRate, false);

        emit ApproveRequest(requestId, newOutRate);
    }
```

```solidity
// RedemptionVault.sol

function approveRequest(uint256 requestId, uint256 newMTokenRate)
        external
        onlyVaultAdmin
    {
        _approveRequest(requestId, newMTokenRate, false);

        emit ApproveRequest(requestId, newMTokenRate);
    }

    
    function safeApproveRequest(uint256 requestId, uint256 newMTokenRate)
        external
        onlyVaultAdmin
    {
        _approveRequest(requestId, newMTokenRate, true);

        emit SafeApproveRequest(requestId, newMTokenRate);
    }
```

This allows users sanctioned by Chainalysis between a request creation and a request approval to still be able to be approved, thus minting/burning mTBILL/mBASIS tokens, when in reality they shouldn’t be able to.

It is important to note that this restriction is only relevant for Chainalysis’ sanctioned users (not greenlisted/blacklisted users). This is because Midas includes a `rejectRequest` function, which allows Midas’ admins to reject any request they don’t think adheres to the protocol’s correct behavior. Checking if a user has been blacklisted or if it’s on the greenlist simply wouldn’t make sense, as the admins would directly reject the request. However, because Chainalysis’ oracle is not operated by Midas’ admins and is fully on-chain, admins could still approve users sanctioned after the deposit/redeem request, without actually noticing they are approving a request from a sanctioned user (the README in Midas’ Sherlock audit page does not mention any off-chain mechanism to monitor sanctioned users). 

In addition, another possible way to still enable sanctioned users to obtain mTBILL/mBASIS tokens is by depositing with a non-blacklisted/non-sanctioned address, and then transferring the tokens to the sanctioned address. This is possible because sanction checks are not enforced at a token level:

```solidity
// mTBILL.sol

function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual override(ERC20PausableUpgradeable) {
        if (to != address(0)) { 
            _onlyNotBlacklisted(from);
            _onlyNotBlacklisted(to);
        }
 
        ERC20PausableUpgradeable._beforeTokenTransfer(from, to, amount);
    }
```

As we can see, transfers ensure that blacklisted users can’t send/receive mTBILL/mBASIS tokens. However, checks are never applied for sanctioned users, so it is still possible to circumvent the sanction checks in the vaults by directly transferring mTBILL/mBASIS to a sanctioned address after depositing on the vault.

## Impact

Sanctioned users can still interact with the protocol. Given the centralized nature of Midas, this vulnerability should be considered of high impact, as it effectively allows users to bypass the KYC requirements for onboarding described in Midas’ [Terms and Conditions](https://2732961456-files.gitbook.io/~/files/v0/b/gitbook-x-prod.appspot.com/o/spaces%2FsPjk0ggBxEJCCnVFFkDR%2Fuploads%2Fc98ICBmy69gcO3P8L5nG%2FMidas%20Terms%20and%20Conditions.pdf?alt=media&token=4373edbe-c2b1-49b1-af78-fe39bc2da800), section 2.2 subsection c), stating that “neither the investor nor the investor’s wallet is listed on neither one of the European Union’s nor on one of the United States of America’s respective sanction lists.”

## Code Snippet

https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/mTBILL/mTBILL.sol#L87-L90

https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/DepositVault.sol#L221

https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVault.sol#L225

## Tool used

Manual Review

## Recommendation

Enforce checks for sanctioned users at a token level. 

```diff
// mTBILL.sol

- contract mTBILL is ERC20PausableUpgradeable, Blacklistable, IMTbill {
+ contract mTBILL is ERC20PausableUpgradeable, Blacklistable, WithSanctionsList, IMTbill {

function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual override(ERC20PausableUpgradeable) {
        if (to != address(0)) { 
            _onlyNotBlacklisted(from);
            _onlyNotBlacklisted(to);
            
+           address _sanctionsList = sanctionsList;
+		        if (_sanctionsList != address(0)) {
+            require(
+                !ISanctionsList(_sanctionsList).isSanctioned(from) && !ISanctionsList(_sanctionsList).isSanctioned(to),
+                "WSL: sanctioned"
+            );
        }
 
        ERC20PausableUpgradeable._beforeTokenTransfer(from, to, amount);
    }
```

If sanctions are required to be checked only on vault interactions, then check if user is sanctioned when approving a mint request.

