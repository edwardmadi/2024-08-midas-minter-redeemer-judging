Muscular Jade Hedgehog

Medium

# Slippage parameter for `RedemptionVaultWIthBUIDL.sol#RedeemInstant` may be unfulfilled.


## Summary

Slippage parameter for `RedemptionVaultWIthBUIDL.sol#RedeemInstant` may be unfulfilled.

## Vulnerability Detail

When users are redeeming from `RedemptionVaultWIthBUIDL`, they may set a slippage parameter `minReceiveAmount`. The `minReceiveAmount` is in 18 decimals, even though the out token must be USDC.

The issue is, when checking the slippage parameter, `amountTokenOutWithoutFee` is used. But during token transfer, `amountTokenOutWithoutFee.convertFromBase18(tokenDecimals).convertToBase18(tokenDecimals)` is used, which is the same as `amountTokenOutWithoutFee / 10**12 * 10**12`.

Since `amountTokenOutWithoutFee / 10**12 * 10**12 < amountTokenOutWithoutFee` due to rounding error, it may occur that `amountTokenOutWithoutFee` fulfills the slippage parameter, but the actual amount doesn't.

```solidity
    /**
     * @notice redeem mToken to USDC if daily limit and allowance not exceeded
     * If contract don't have enough USDC, BUIDL redemption flow will be triggered
     * Burns mToken from the user.
     * Transfers fee in mToken to feeReceiver
     * Transfers tokenOut to user.
     * @param tokenOut token out address, always ignore
     * @param amountMTokenIn amount of mToken to redeem
     * @param minReceiveAmount minimum expected amount of tokenOut to receive (decimals 18)
     */
    function redeemInstant(
        address tokenOut,
        uint256 amountMTokenIn,
        uint256 minReceiveAmount
    )
        external
        override
        whenFnNotPaused(this.redeemInstant.selector)
        onlyGreenlisted(msg.sender)
        onlyNotBlacklisted(msg.sender)
        onlyNotSanctioned(msg.sender)
    {
        ...

        uint256 amountTokenOutWithoutFee = (amountMTokenWithoutFee *
            mTokenRate) / tokenOutRate;

        require(
@>          amountTokenOutWithoutFee >= minReceiveAmountCopy,
            "RVB: minReceiveAmount > actual"
        );

        uint256 amountTokenOutWithoutFeeFrom18 = amountTokenOutWithoutFee
            .convertFromBase18(tokenDecimals);

        _checkAndRedeemBUIDL(tokenOutCopy, amountTokenOutWithoutFeeFrom18);

        _tokenTransferToUser(
            tokenOutCopy,
            user,
@>          amountTokenOutWithoutFeeFrom18.convertToBase18(tokenDecimals),
            tokenDecimals
        );

        emit RedeemInstant(
            user,
            tokenOutCopy,
            amountMTokenInCopy,
            feeAmount,
            amountTokenOutWithoutFee
        );
    }
```

## Impact

Users may receive less tokens than the slippage parameter.

## Code Snippet

- https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVaultWithBUIDL.sol#L132-L135

## Tool used

Manual Review

## Recommendation

Change the slippage check to use the actual transferred amount.