Helpful Wooden Porpoise

High

# It doesn't check if the user is blacklisted or sanctioned when the admin approves the request

## Summary
It doesn't check if the user is blacklisted or sanctioned when the admin approves the request
## Vulnerability Detail
Midas claimed that [it is fully compliant with European securities regulations](https://docs.midas.app/#regulatory-compliance):  
> Our tokens, mBASIS and mTBILL, are fully compliant with European securities regulations, ensuring that all holders possess legal rights to the assets. These tokens are structured under German law and issued as debt securities.  

When a user request mToken minting or redeeming, it will check if the user is blacklisted or sanctioned:
https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/DepositVault.sol:
```solidity
    function depositInstant(
        address tokenIn,
        uint256 amountToken,
        uint256 minReceiveAmount,
        bytes32 referrerId
    )
        external
        whenFnNotPaused(this.depositInstant.selector)
        onlyGreenlisted(msg.sender)
@>      onlyNotBlacklisted(msg.sender)//@audit-info blacklist check
@>      onlyNotSanctioned(msg.sender)//@audit-info sanction check
    {
        ...
    }

    function depositRequest(
        address tokenIn,
        uint256 amountToken,
        bytes32 referrerId
    )
        external
        whenFnNotPaused(this.depositRequest.selector)
        onlyGreenlisted(msg.sender)
@>      onlyNotBlacklisted(msg.sender)//@audit-info blacklist check
@>      onlyNotSanctioned(msg.sender)//@audit-info sanction check
        returns (uint256 requestId)
    {
        ...
    }
```
https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVault.sol:
```solidity
    function redeemInstant(
        address tokenOut,
        uint256 amountMTokenIn,
        uint256 minReceiveAmount
    )
        external
        virtual
        whenFnNotPaused(this.redeemInstant.selector)
        onlyGreenlisted(msg.sender)
@>      onlyNotBlacklisted(msg.sender)//@audit-info blacklist check
@>      onlyNotSanctioned(msg.sender)//@audit-info sanction check
        returns (uint256 requestId)
    {
        ...
    }

    function redeemRequest(address tokenOut, uint256 amountMTokenIn)
        external
        whenFnNotPaused(this.redeemRequest.selector)
        onlyGreenlisted(msg.sender)
@>      onlyNotBlacklisted(msg.sender)//@audit-info blacklist check
@>      onlyNotSanctioned(msg.sender)//@audit-info sanction check
        returns (uint256 requestId)
    {
        require(tokenOut != MANUAL_FULLFILMENT_TOKEN, "RV: tokenOut == fiat");
        return _redeemRequest(tokenOut, amountMTokenIn);
    }

    function redeemFiatRequest(uint256 amountMTokenIn)
        external
        whenFnNotPaused(this.redeemFiatRequest.selector)
        onlyAlwaysGreenlisted(msg.sender)
@>      onlyNotBlacklisted(msg.sender)//@audit-info blacklist check
@>      onlyNotSanctioned(msg.sender)//@audit-info sanction check
        returns (uint256 requestId)
    {
        return _redeemRequest(MANUAL_FULLFILMENT_TOKEN, amountMTokenIn);
    }
```
As we can see, neither blacklisted user nor sanctioned user can request mToken minting or redeeming.
However, since a user could be blacklisted or sanctioned after submitting a standard minting/redeeming request, the protocol should ensure that:
- `mToken` should not be minted to the blacklisted/sanctioned user
- `paymentToken` should not be transferred to the blacklisted/sanctioned user

But it doesn't check if the request sender is blacklisted or sanctioned when the admin approves the request, which puts the protocol at risk of violating regulations.

## Impact
The Midas protocol could violate regulations accidentally. 
## Code Snippet
https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/DepositVault.sol#L303-L329
https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVault.sol#L313-L348
## Tool used

Manual Review

## Recommendation
Check if the request sender is blacklisted or sanctioned when approving the request:
https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/DepositVault.sol:
```diff
    function _approveRequest(
        uint256 requestId,
        uint256 newOutRate,
        bool isSafe
    ) private {
        Request memory request = mintRequests[requestId];

        require(request.sender != address(0), "DV: request not exist");
        require(
            request.status == RequestStatus.Pending,
            "DV: request not pending"
        );
+       _onlyNotBlacklisted(request.sender);
+       address _sanctionsList = sanctionsList;
+       if (_sanctionsList != address(0)) {
+           require(
+               !ISanctionsList(_sanctionsList).isSanctioned(request.sender),
+               "WSL: sanctioned"
+           );
+       }

        if (isSafe)
            _requireVariationTolerance(request.tokenOutRate, newOutRate);

        uint256 amountMToken = (request.usdAmountWithoutFees * (10**18)) /
            newOutRate;

        mToken.mint(request.sender, amountMToken);

        totalMinted[request.sender] += amountMToken;

        request.status = RequestStatus.Processed;
        request.tokenOutRate = newOutRate;
        mintRequests[requestId] = request;
    }
```
https://github.com/sherlock-audit/2024-08-midas-minter-redeemer/blob/main/midas-contracts/contracts/RedemptionVault.sol
```diff
    function _approveRequest(
        uint256 requestId,
        uint256 newMTokenRate,
        bool isSafe
    ) internal {
        Request memory request = redeemRequests[requestId];

        _validateRequest(request.sender, request.status);
+       _onlyNotBlacklisted(request.sender);
+       address _sanctionsList = sanctionsList;
+       if (_sanctionsList != address(0)) {
+           require(
+               !ISanctionsList(_sanctionsList).isSanctioned(request.sender),
+               "WSL: sanctioned"
+           );
+       }
        if (isSafe) {
            _requireVariationTolerance(request.mTokenRate, newMTokenRate);
        }

        mToken.burn(address(this), request.amountMToken);

        if (request.tokenOut != MANUAL_FULLFILMENT_TOKEN) {
            uint256 tokenDecimals = _tokenDecimals(request.tokenOut);

            uint256 amountTokenOutWithoutFee = _truncate(
                (request.amountMToken * newMTokenRate) / request.tokenOutRate,
                tokenDecimals
            );

            _tokenTransferFromTo(
                request.tokenOut,
                requestRedeemer,
                request.sender,
                amountTokenOutWithoutFee,
                tokenDecimals
            );
        }

        request.status = RequestStatus.Processed;
        request.mTokenRate = newMTokenRate;
        redeemRequests[requestId] = request;
    }
```